plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.gones.foodinventorykotlin"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", 'BASE_URL', "\"https://fr.openfoodfacts.org/api/v0/\""

        try {
            Properties secretProperties = new Properties()
            secretProperties.load(project.rootProject.file('secrets.properties').newDataInputStream())
            
            buildConfigField "String", 'SUPABASE_URL', "\"${secretProperties.getProperty('SUPABASE_URL')}\""
            buildConfigField "String", 'SUPABASE_KEY', "\"${secretProperties.getProperty('SUPABASE_KEY')}\""
            buildConfigField "String", 'APP_CENTER_KEY', "\"${secretProperties.getProperty('APP_CENTER_KEY')}\""
        }
        catch (Exception ignored) {

        }
    }

    buildTypes {
        appcenter {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", 'SUPABASE_URL', "\"${System.env.SUPABASE_URL}\""
            buildConfigField "String", 'SUPABASE_KEY', "\"${System.env.SUPABASE_KEY}\""
            buildConfigField "String", 'APP_CENTER_KEY', "\"${System.env.APP_CENTER_KEY}\""
        }

        debug {
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.6'
    }
    namespace 'com.gones.foodinventorykotlin'
}

dependencies {
    // Version
    def koin_version = '3.5.3'
    def retrofit_version = "2.9.0"
    def logging_interceptor_version = '5.0.0-alpha.12'
    def navigation_version = '2.7.6'
    def supabase_version = '2.0.3'
    def nav_version = '2.7.6'
    def coil_version = '2.5.0'
    def camera_version = '1.3.1'
    def room_version = '2.6.1'
    def glide_version = '4.16.0'
    def compose_version = '1.2.0-beta01'
    def lifecycle_version = "2.6.2"
    def appCenterSdkVersion = '4.4.5'

    implementation 'androidx.annotation:annotation:1.7.1'
    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.9.22'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'io.ktor:ktor-client-android:2.3.7'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.2'

    // Splash Screen
    implementation "androidx.core:core-splashscreen:1.0.1"

    // Compose
    implementation "androidx.compose.material3:material3:$compose_version"
    implementation 'androidx.compose.material:material-icons-core'

    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0'

    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.8.2'

    /* CameraX */
    implementation "androidx.camera:camera-camera2:$camera_version"
    implementation "androidx.camera:camera-lifecycle:$camera_version"
    implementation "androidx.camera:camera-view:$camera_version"

    /* Navigation */
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-compose:$nav_version"

    /* Koin */
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-compose:$koin_version"

    /* Retrofit */
    implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    /* Room */
    implementation("androidx.room:room-runtime:$room_version")
    implementation("androidx.room:room-ktx:$room_version")
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt("androidx.room:room-compiler:$room_version")

    /* ML Kit */
    implementation 'com.google.mlkit:barcode-scanning:17.2.0'

    /* Glide */
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    /* Timber */
    implementation 'com.jakewharton.timber:timber:5.0.1'

    /* Supabase */
    implementation("io.github.jan-tennert.supabase:postgrest-kt:$supabase_version")
    implementation "io.github.jan-tennert.supabase:gotrue-kt:$supabase_version"

    /* Coil */
    implementation("io.coil-kt:coil-compose:$coil_version")

    /* DataStore */
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    /* AppCenter */
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    // UI Tests
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}